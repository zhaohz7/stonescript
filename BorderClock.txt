
//////////////////////////////////////////////////

//Made by: Agerak
//V1.0 Displays analog time points around the edge of the screen
//V1.1 Smoothed animation of the 'hands'
//V2.0 Redesigned the cardinal points and rotation speed to be more uniform
//V2.1 added subtle markers at the hours and interaction of hands colors

//////////////////////////////////////////////////

// To use:
//var show = new ui\BorderClockV2
//show.Clock(#0000FF,#00FF00,#FF0000)

// May adjust the colors in the call, above is Blue hour, Green minute, Red second
// Recommended to put at the end of your mindstone so that it will display over other overlays

//////////////////////////////////////////////////
func Output(x, y, color, a)
  ?1//?this.draw=1 // toggle control for drawing overlay
    >`@x@,@y@,@color@,@a@
//////////////////////////////////////////////////

var prevsec=0
var frametime=0

func Clock(h,m,s)
// h = hour color
// m = minute color
// s = second color

  var total
  total=screen.w*2+screen.h*2 // total slots around border

  ?prevsec!time.second
    frametime=0
    prevsec=time.second

  var sec
  sec=total*time.second/60+(total*(time.second+1)/60-total*time.second/60)*frametime/30

  var min
  min=total*time.minute/60+(total*(time.minute+1)/60-total*time.minute/60)*time.second/60

  var hour
  hour=total*(time.hour%12)/12+(total*((time.hour%12)+1)/12-total*(time.hour%12)/12)*time.minute/60

  draw.bg(screen.w/2,0,#white)
  Output(screen.w/2,0,#000000,X)
  draw.bg(screen.w-1,screen.h/2,#808080)
  Output(screen.w-1,screen.h/2,#000000,3)
  draw.bg(screen.w/2,screen.h-1,#808080)
  Output(screen.w/2,screen.h-1,#000000,6)
  draw.bg(0,screen.h/2,#808080)
  Output(0,screen.h/2,#000000,9)
  draw.bg(screen.w/2+(screen.w+screen.h)/6,0,#404040)
  draw.bg(screen.w/2+(screen.w+screen.h)/3,0,#404040)
  draw.bg(screen.w/2-(screen.w+screen.h)/6,0,#404040)
  draw.bg(screen.w/2-(screen.w+screen.h)/3,0,#404040)
  draw.bg(screen.w/2+(screen.w+screen.h)/6,screen.h-1,#404040)
  draw.bg(screen.w/2+(screen.w+screen.h)/3,screen.h-1,#404040)
  draw.bg(screen.w/2-(screen.w+screen.h)/6,screen.h-1,#404040)
  draw.bg(screen.w/2-(screen.w+screen.h)/3,screen.h-1,#404040)

// Adjustment for top center noon/0
  sec=(sec+screen.w/2)%total
  min=(min+screen.w/2)%total
  hour=(hour+screen.w/2)%total

  ?h="#0000FF"&m="#00FF00"&s="#FF0000"
    ?hour=min&hour=sec
      h="#rainFF"
    :?hour=min
      h="#00FFFF"
    :?hour=sec
      h="#FF00FF"
    :?min=sec
      m="#FFFF00"
  ?hour=min&hour=sec
    h="#rainFF"

  ?sec<screen.w
    Output(sec,0,s,S)
  :
    sec=sec-screen.w
    ?sec<screen.h
      Output(screen.w-1,sec,s,S)
    :
      sec=sec-screen.h
      ?sec<screen.w
        Output(screen.w-1-sec,screen.h-1,s,S)
      :
        sec=sec-screen.w
        ?sec<screen.h
          Output(0,screen.h-1-sec,s,S)
        :
          WTF=1

  ?min<screen.w
    Output(min,0,m,M)
  :
    min=min-screen.w
    ?min<screen.h
      Output(screen.w-1,min,m,M)
    :
      min=min-screen.h
      ?min<screen.w
        Output(screen.w-1-min,screen.h-1,m,M)
      :
        min=min-screen.w
        ?min<screen.h
          Output(0,screen.h-1-min,m,M)
        :
          WTF=1

  ?hour<screen.w
    Output(hour,0,h,H)
  :
    hour=hour-screen.w
    ?hour<screen.h
      Output(screen.w-1,hour,h,H)
    :
      hour=hour-screen.h
      ?hour<screen.w
        Output(screen.w-1-hour,screen.h-1,h,H)
      :
        hour=hour-screen.w
        ?hour<screen.h
          Output(0,screen.h-1-hour,h,H)
        :
          WTF=1

  frametime++